OSWRCH = $FFEE

xvar_index   = $70
counter      = $71
multiplier   = $72
multiplicand = $76
product      = $7a
result_index        = $82
product_index       = $82
param1_index        = $83
multiplicand_index  = $83
multiplier_index    = $84

* = $1900

; ***************************************************************************************
main
    jsr set_mode                        ;
    jmp draw_ellipse                    ;

; ***************************************************************************************
draw_ellipse
    lda #a - variables_start            ;
    ldx #a - variables_start            ;
    ldy #aa - variables_start           ;
    jsr multiply16                      ; AA = A*A

    lda #b - variables_start            ;
    ldx #b - variables_start            ;
    ldy #bb - variables_start           ;
    jsr multiply16                      ; BB = B*B

    lda #0                              ;
    ldx #yyaa - variables_start         ;
    jsr set_variable32_to_a             ; YYAA = 0

    lda #aa - variables_start           ;
    ldx #bb - variables_start           ;
    ldy #aabb - variables_start         ;
    jsr multiply32                      ; AABB = AA*BB

    ldx #aa - variables_start           ;
    ldy #yyaa_diff - variables_start    ;
    jsr copy32                          ; YYAA_DIFF = AA


    ldx #a - variables_start            ;
    ldy #x1 - variables_start           ;
    jsr negate16                        ; X1 = -A

    lda #a - variables_start            ;
    ldx #b - variables_start            ;
    ldy #t2 - variables_start           ;
    jsr multiply16                      ; T2 = A*B

    ldx #t2 - variables_start           ;
    ldy #t1 - variables_start           ;
    jsr negate32                        ; T1 = -T2

    ldx #aabb - variables_start         ;
    ldy #tt1 - variables_start          ;
    jsr copy32                          ; TT1 = AABB

    lda #b - variables_start            ;
    ldx #t1 - variables_start           ;
    ldy #bt1 - variables_start          ;
    jsr multiply32                      ; BT1 = B*T1

    lda #s - variables_start            ;
    ldx #t1 - variables_start           ;
    ldy #st1 - variables_start          ;
    jsr multiply32                      ; ST1 = S*T1


    ldx #a - variables_start            ;
    ldy #x2 - variables_start           ;
    jsr copy16                          ; X2 = A

    ldx #aabb - variables_start         ;
    ldy #tt2 - variables_start          ;
    jsr copy32                          ; TT2=AABB

    ldx #bt1 - variables_start          ;
    ldy #bt2 - variables_start          ;
    jsr negate32                        ; BT2=-BT1

    ldx #st1 - variables_start          ;
    ldy #st2 - variables_start          ;
    jsr negate32                        ; ST2=-ST1

    lda #0                              ;
    ldx #ys - variables_start           ;
    jsr set_variable32_to_a             ; YS = 0

    lda #0                              ;
    ldx #y - variables_start            ;
    jsr set_variable16_to_a             ; Y = 0

    ldx #bb - variables_start           ;
    ldy #bb2 - variables_start          ;
    jsr div2_32                         ; BB2 = BB/2

    lda #s - variables_start            ;
    ldx #b - variables_start            ;
    ldy #sb - variables_start           ;
    jsr multiply32                      ; SB = S*B

    lda #s - variables_start            ;
    ldx #s - variables_start            ;
    ldy #ss - variables_start           ;
    jsr multiply32                      ; SS = S*S

fory_loop
    ldx #x1 - variables_start           ;
    ldy #ox1 - variables_start          ;
    jsr copy16                          ; OX1 = X1

    ldx #x2 - variables_start           ;
    ldy #ox2 - variables_start          ;
    jsr copy16                          ; OX2 = X2

    lda #aabb - variables_start         ;
    ldx #yyaa - variables_start         ;
    ldy #dd - variables_start           ; DD=AABB-YYAA
    jsr subtract32                      ;

repeat_loop1
    lda t2 + 3                          ;
    bpl ++                              ;
    jmp inc_x2_t2                       ; IF T2<0 THEN GOTO ...
++
    ora t2 + 2                          ;
    ora t2 + 1                          ;
    ora t2 + 0                          ;
    beq +                               ; IF T2=0 THEN GOTO ...

    lda #tt2 - variables_start          ;
    ldx #dd - variables_start           ;
    ldy #d1 - variables_start           ;
    jsr subtract32                      ; D1=TT2-DD

    lda #bt2 - variables_start          ;
    ldx #bb2 - variables_start          ;
    ldy #bd - variables_start           ;
    jsr add32                           ; BD=BT2+BB2

    lda #bd - variables_start           ;
    ldx #d1 - variables_start           ;
    ldy #temp - variables_start         ;
    jsr subtract32                      ; temp = BD-D1

    lda temp+3                          ;
    bpl ++                              ;
    jmp dec_x2_t2                       ; IF temp<0 THEN GOTO ...
++

    lda #bd - variables_start           ;
    ldx #d1 - variables_start           ;
    ldy #temp - variables_start         ;
    jsr add32                           ; temp = BD+D1

    lda temp+3                          ;
    bpl ++                              ;
    jmp inc_x2_t2                       ; IF temp<0 THEN GOTO ...
++
+
    ; DX%=SGN(X2%-OX2%)
    lda #x2 - variables_start           ;
    ldx #ox2 - variables_start          ;
    ldy #temp - variables_start         ; temp = X2-OX2
    jsr subtract16                      ;

    ldx #temp - variables_start         ;
    jsr sgn16                           ; temp=SGN(temp)

    lda #ox2 - variables_start          ;
    ldx #x2 - variables_start           ;
    jsr draw_hlines                     ;

    ; second half!
repeat_loop2
    lda t1 + 3                          ;
    bmi ++                              ;
    ora t1 + 2                          ;
    ora t1 + 1                          ;
    ora t1 + 0                          ;
    beq +                               ; IF T=0 THEN GOTO ...
    jmp dec_x1_t1                       ; IF T>0 THEN GOTO ...
++

    lda #tt1 - variables_start          ;
    ldx #dd - variables_start           ;
    ldy #d1 - variables_start           ;
    jsr subtract32                      ; D1=TT-DD

    lda #bb2 - variables_start          ;
    ldx #bt1 - variables_start          ;
    ldy #bd - variables_start           ;
    jsr subtract32                      ; BD=BB2-BT1

    lda #bd - variables_start           ;
    ldx #d1 - variables_start           ;
    ldy #temp - variables_start         ;
    jsr subtract32                      ; temp = BD-D1

    lda temp+3                          ;
    bpl ++                              ;
    jmp inc_x1_t1                       ;
++

    lda #bd - variables_start           ;
    ldx #d1 - variables_start           ;
    ldy #temp - variables_start         ;
    jsr add32                           ; temp = BD+D1

    lda temp+3                          ;
    bpl ++                              ;
    jmp dec_x1_t1                       ;
++
+
    ; DX%=SGN(X1%-OX1%)
    lda #x1 - variables_start           ;
    ldx #ox1 - variables_start          ;
    ldy #temp - variables_start         ; temp = X1-OX1
    jsr subtract16                      ;

    ldx #temp - variables_start         ;
    jsr sgn16                           ; temp=SGN(temp)

    lda #ox1 - variables_start          ;
    ldx #x1 - variables_start           ;
    jsr draw_hlines                     ;

    lda #tt2 - variables_start          ;
    ldx #st2 - variables_start          ;
    ldy #tt2 - variables_start          ;
    jsr subtract32                      ; TT2-=ST2

    lda #tt2 - variables_start          ;
    ldx #st2 - variables_start          ;
    ldy #tt2 - variables_start          ;
    jsr subtract32                      ; TT2-=ST2    (again)

    lda #tt2 - variables_start          ;
    ldx #ss - variables_start           ;
    ldy #tt2 - variables_start          ;
    jsr add32                           ; TT2+=SS


    lda #tt1 - variables_start          ;
    ldx #st1 - variables_start          ;
    ldy #tt1 - variables_start          ;
    jsr subtract32                      ; TT1-=ST1

    lda #tt1 - variables_start          ;
    ldx #st1 - variables_start          ;
    ldy #tt1 - variables_start          ;
    jsr subtract32                      ; TT1-=ST1    (again)

    lda #tt1 - variables_start          ;
    ldx #ss - variables_start           ;
    ldy #tt1 - variables_start          ;
    jsr add32                           ; TT1+=SS

    lda #ys - variables_start           ;
    ldx #s - variables_start            ;
    ldy #ys - variables_start           ;
    jsr add32                           ; YS+=S

    lda #st2 - variables_start          ;
    ldx #ss - variables_start           ;
    ldy #st2 - variables_start          ;
    jsr subtract32                      ; ST2-=SS

    lda #st1 - variables_start          ;
    ldx #ss - variables_start           ;
    ldy #st1 - variables_start          ;
    jsr subtract32                      ; ST1-=SS

    lda #yyaa - variables_start         ;
    ldx #yyaa_diff - variables_start    ;
    ldy #yyaa - variables_start         ;
    jsr add32                           ; YYAA+=YYA_DIFF

    lda #yyaa_diff - variables_start    ;
    ldx #aa - variables_start           ;
    ldy #yyaa_diff - variables_start    ;
    jsr add32                           ; YYAA_DIFF+=AA

    lda #yyaa_diff - variables_start    ;
    ldx #aa - variables_start           ;
    ldy #yyaa_diff - variables_start    ;
    jsr add32                           ; YYAA_DIFF+=AA   (again)

    lda #bt2 - variables_start          ;
    ldx #sb - variables_start           ;
    ldy #bt2 - variables_start          ;
    jsr subtract32                      ; BT2-=SB

    lda #bt1 - variables_start          ;
    ldx #sb - variables_start           ;
    ldy #bt1 - variables_start          ;
    jsr subtract32                      ; BT1-=SB

    lda #t2 - variables_start           ;
    ldx #s - variables_start            ;
    ldy #t2 - variables_start           ;
    jsr subtract32                      ; T2-=S

    lda #t1 - variables_start           ;
    ldx #s - variables_start            ;
    ldy #t1 - variables_start           ;
    jsr subtract32                      ; T1-=S

    inc y                               ;
    bne +                               ;
    inc y+1                             ; Y=Y+1
+
    lda #b - variables_start            ;
    ldx #y - variables_start            ;
    ldy #temp - variables_start         ;
    jsr subtract16                      ; temp=B-Y
    lda temp + 1                        ;
    bmi +                               ;
    ora temp                            ;
    beq +                               ;
    jmp fory_loop                       ; IF (B - Y) > 0 GOTO fory_loop
+

    ; plot the final line
    ; DX%=SGN(X2%-X1%):OX1%=X1%:X1%=X2%-DX%
    lda #x2 - variables_start           ;
    ldx #x1 - variables_start           ;
    ldy #temp - variables_start         ; temp = X2-OX2
    jsr subtract16                      ;
    ldx #temp - variables_start         ;
    jsr sgn16                           ; temp=SGN(temp)

    ldx #x1 - variables_start           ;
    ldy #ox1 - variables_start          ;
    jsr copy16                          ; OX1=X1

    lda #x2 - variables_start           ;
    ldx #temp - variables_start         ;
    ldy #x1 - variables_start           ;
    jsr subtract16                      ; X1=X2-temp

    lda #ox1 - variables_start          ;
    ldx #x1 - variables_start           ;
    jmp draw_hlines                     ; draw final lines

; ***************************************************************************************
inc_x2_t2
    inc x2                              ;
    bne +                               ;
    inc x2 + 1                          ; X2=X2+1
+
    lda #t2 - variables_start           ;
    ldx #b - variables_start            ;
    ldy #t2 - variables_start           ;
    jsr add32                           ; T2+=B

    lda #tt2 - variables_start          ;
    ldx #bt2 - variables_start          ;
    ldy #tt2 - variables_start          ;
    jsr add32                           ; TT2+=BT2

    lda #tt2 - variables_start          ;
    ldx #bt2 - variables_start          ;
    ldy #tt2 - variables_start          ;
    jsr add32                           ; TT2+=BT2 (again)

    lda #tt2 - variables_start          ;
    ldx #bb - variables_start           ;
    ldy #tt2 - variables_start          ;
    jsr add32                           ; TT2+=BB

    lda #bt2 - variables_start          ;
    ldx #bb - variables_start           ;
    ldy #bt2 - variables_start          ;
    jsr add32                           ; BT2+=BB

    lda #st2 - variables_start          ;
    ldx #sb - variables_start           ;
    ldy #st2 - variables_start          ;
    jsr add32                           ; ST2+=SB

    jmp repeat_loop1                    ;

; ***************************************************************************************
dec_x2_t2
    lda x2                              ;
    bne +                               ;
    dec x2 + 1                          ; X2-=1
+
    dec x2                              ;

    lda #t2 - variables_start           ;
    ldx #b - variables_start            ;
    tay                                 ; T2-=B
    jsr subtract32                      ;

    lda #tt2 - variables_start          ;
    ldx #bt2 - variables_start          ;
    ldy #tt2 - variables_start          ;
    jsr subtract32                      ; TT2-=BT2

    lda #tt2 - variables_start          ;
    ldx #bt2 - variables_start          ;
    ldy #tt2 - variables_start          ;
    jsr subtract32                      ; TT2-=BT2 (again)

    lda #tt2 - variables_start          ;
    ldx #bb - variables_start           ;
    ldy #tt2 - variables_start          ;
    jsr add32                           ; TT2+=BB

    lda #bt2 - variables_start          ;
    ldx #bb - variables_start           ;
    ldy #bt2 - variables_start          ;
    jsr subtract32                      ; BT2-=BB

    lda #st2 - variables_start          ;
    ldx #sb - variables_start           ;
    ldy #st2 - variables_start          ;
    jsr subtract32                      ; ST2-=SB
    jmp repeat_loop1                    ;

; ***************************************************************************************
inc_x1_t1
    inc x1                              ;
    bne +                               ;
    inc x1 + 1                          ; X1+=1
+
    lda #t1 - variables_start           ;
    ldx #b - variables_start            ;
    ldy #t1 - variables_start           ;
    jsr add32                           ; T1+=B

    lda #tt1 - variables_start          ;
    ldx #bt1 - variables_start          ;
    ldy #tt1 - variables_start          ;
    jsr add32                           ; TT1+=BT1

    lda #tt1 - variables_start          ;
    ldx #bt1 - variables_start          ;
    ldy #tt1 - variables_start          ;
    jsr add32                           ; TT1+=BT1 (again)

    lda #tt1 - variables_start          ;
    ldx #bb - variables_start           ;
    ldy #tt1 - variables_start          ;
    jsr add32                           ; TT1+=BB

    lda #bt1 - variables_start          ;
    ldx #bb - variables_start           ;
    ldy #bt1 - variables_start          ;
    jsr add32                           ; BT1+=BB

    lda #st1 - variables_start          ;
    ldx #sb - variables_start           ;
    ldy #st1 - variables_start          ;
    jsr add32                           ; ST1+=SB

    jmp repeat_loop2                    ;

; ***************************************************************************************
dec_x1_t1
    lda x1                              ;
    bne +                               ;
    dec x1 + 1                          ; X1-=1
+
    dec x1                              ;

    lda #t1 - variables_start           ;
    ldx #b - variables_start            ;
    tay                                 ; T1-=B
    jsr subtract32                      ;

    lda #tt1 - variables_start          ;
    ldx #bt1 - variables_start          ;
    ldy #tt1 - variables_start          ;
    jsr subtract32                      ; TT1-=BT1

    lda #tt1 - variables_start          ;
    ldx #bt1 - variables_start          ;
    ldy #tt1 - variables_start          ;
    jsr subtract32                      ; TT1-=BT1 (again)

    lda #tt1 - variables_start          ;
    ldx #bb - variables_start           ;
    ldy #tt1 - variables_start          ;
    jsr add32                           ; TT1+=BB

    lda #bt1 - variables_start          ;
    ldx #bb - variables_start           ;
    ldy #bt1 - variables_start          ;
    jsr subtract32                      ; BT1-=BB

    lda #st1 - variables_start          ;
    ldx #sb - variables_start           ;
    ldy #st1 - variables_start          ;
    jsr subtract32                      ; ST1-=SB
    jmp repeat_loop2                    ;

; ***************************************************************************************
draw_hlines
    stx xvar_index                      ; index for X1 or X2, lets say X2 for these comments
    ldx #temp - variables_start         ;
    ldy #temp - variables_start         ;
    jsr add16                           ; temp = temp + A (either OX1 or OX2)

    ; IF temp == x2 then plot 69
    jsr draw_hline                      ;

    ldx #temp - variables_start         ;
    ldy #temp - variables_start         ;
    jsr negate16                        ; temp=-temp
    ldx xvar_index                      ;
    ldy xvar_index                      ;
    jsr negate16                        ; X2 = -X2
    ldx #y - variables_start            ;
    ldy #y - variables_start            ;
    jsr negate16                        ; Y = -Y

    jsr draw_hline

    ldx xvar_index                      ;
    ldy xvar_index                      ;
    jsr negate16                        ; X2 = -X2

    ldx #y - variables_start            ;
    ldy #y - variables_start            ;
    jmp negate16                        ; Y = -Y

; ***************************************************************************************
draw_hline
    ; IF temp == x2 then plot 69
    lda temp                            ;
    ldx xvar_index                      ;
    cmp variables_start,X               ;
    bne +                               ;
    lda temp + 1                        ;
    cmp variables_start + 1,X           ;
    bne +                               ;
    lda #69                             ; PLOT 69 for plotting a point
    bne ++                              ; ALWAYS branch
+
    lda #4                              ;
    ldx #temp - variables_start         ;
    ldy #y - variables_start            ;
    jsr plot_xy                         ; MOVE  temp*4, Y*4
    lda #5                              ;
++
    ldx xvar_index                      ;
    ldy #y - variables_start            ;
    jmp plot_xy                         ; DRAW  X2*4, Y*4

; ***************************************************************************************
set_mode
    lda #22                             ;
    jsr OSWRCH                          ;
    lda #1                              ;
    jmp OSWRCH                          ;

; ***************************************************************************************
;
; Y = A * X
;
; multiplies two 32 bit unsigned integers into a 32 bit result
;
; On Entry:
;   The registers are the byte offsets from variables_start to the variables:
;   A = multiplier      (4 bytes)
;   X = multiplicand    (4 bytes)
;   Y = result          (4 bytes)
;
; ***************************************************************************************
multiply32
    sta multiplier_index                ;
    stx multiplicand_index              ;
    sty product_index                   ;

    tax
    ldy #multiplier                     ;
    jsr copy4_to_zp                     ;

    ldx multiplicand_index              ;
    ldy #multiplicand                   ;
    jsr copy4_to_zp                     ;

    lda #32                             ; set binary count to 32
    sta counter                         ;
    lda #0                              ;
    sta product + 4                     ; clear upper half of product
    sta product + 5                     ;
    sta product + 6                     ;
    sta product + 7                     ;
-
    lsr multiplier + 3                  ; shift multiplier right
    ror multiplier + 2                  ;
    ror multiplier + 1                  ;
    ror multiplier + 0                  ;
    bcc +                               ; go rotate right if c = 0

    lda product + 4                     ; get upper half of product and add multiplicand to it
    clc                                 ;
    adc multiplicand                    ;
    sta product + 4                     ;

    lda product + 5                     ;
    adc multiplicand + 1                ;
    sta product + 5                     ;

    lda product + 6                     ;
    adc multiplicand + 2                ;
    sta product + 6                     ;

    lda product + 7                     ;
    adc multiplicand + 3                ;

+
    ror                                 ; rotate partial product right
    sta product + 7                     ;
    ror product + 6                     ;
    ror product + 5                     ;
    ror product + 4                     ;
    ror product + 3                     ;
    ror product + 2                     ;
    ror product + 1                     ;
    ror product + 0                     ;
    dec counter                         ; decrement bit count and
    bne -                               ; loop until 32 bits are done

    ; copy result (4 bytes)
    ldx product_index                   ;
    ldy #3                              ;
-
    lda product,Y                       ;
    sta variables_start + 3,X           ;
    dex                                 ;
    dey                                 ;
    bpl -                               ;
    rts                                 ;

; ***************************************************************************************
plot_block
    !word 0                             ; NOTE: These bytes are reversed
    !word 0                             ; when sending to the VDU drivers
    !byte 5                             ; PLOT type
    !byte 25                            ; VDU 25 for PLOT

; ***************************************************************************************
plot_xy
    sta plot_block + 4                  ;
    lda variables_start,X               ;
    clc                                 ;
    adc centrex                         ;
    sta plot_block + 3                  ;
    lda variables_start + 1,X           ;
    adc centrex + 1                     ;
    sta plot_block + 2                  ;
    lda variables_start,Y               ;
    clc                                 ;
    adc centrey                         ;
    sta plot_block + 1                  ;
    lda variables_start + 1,Y           ;
    adc centrey + 1                     ;
    sta plot_block + 0                  ;

    asl plot_block + 1                  ;
    rol plot_block                      ;
    asl plot_block + 1                  ;
    rol plot_block                      ; multiply coordinates by four
    asl plot_block + 3                  ;
    rol plot_block + 2                  ;
    asl plot_block + 3                  ;
    rol plot_block + 2                  ;

    ldx #5                              ;
-
    lda plot_block,X                    ;
    jsr OSWRCH                          ;
    dex                                 ;
    bpl -                               ;
    rts                                 ;

; ***************************************************************************************
copy4_to_zp
    lda variables_start,X               ;
    sta $00,Y                           ;
    lda variables_start + 1,X           ;
    sta $01,Y                           ;
    lda variables_start + 2,X           ;
    sta $02,Y                           ;
    lda variables_start + 3,X           ;
    sta $03,Y                           ;
    rts                                 ;

; ***************************************************************************************
copy2_to_zp
    lda variables_start,X               ;
    sta $00,Y                           ;
    lda variables_start + 1,X           ;
    sta $01,Y                           ;
    rts                                 ;

; ***************************************************************************************
set_variable16_to_a
    sta variables_start,X               ;
    lda #0                              ;
    sta variables_start + 1,X           ;
    rts                                 ;

; ***************************************************************************************
set_variable32_to_a
    sta variables_start,X               ;
    lda #0                              ;
    sta variables_start + 1,X           ;
    sta variables_start + 2,X           ;
    sta variables_start + 3,X           ;
    rts                                 ;

; ***************************************************************************************
;
; Y = -X
;
; ***************************************************************************************
negate16
    lda #0                              ;
    sec                                 ;
    sbc variables_start,X               ;
    sta variables_start,Y               ;
    lda #0                              ;
    sbc variables_start + 1,X           ;
    sta variables_start + 1,Y           ;
    rts                                 ;

; ***************************************************************************************
;
; Y = -X
;
; ***************************************************************************************
negate32
    lda #0                              ;
    sec                                 ;
    sbc variables_start,X               ;
    sta variables_start,Y               ;
    lda #0                              ;
    sbc variables_start + 1,X           ;
    sta variables_start + 1,Y           ;
    lda #0                              ;
    sbc variables_start + 2,X           ;
    sta variables_start + 2,Y           ;
    lda #0                              ;
    sbc variables_start + 3,X           ;
    sta variables_start + 3,Y           ;
    rts                                 ;

; ***************************************************************************************
;
; Y = X
;
; ***************************************************************************************
copy32
    lda variables_start + 3,X           ;
    sta variables_start + 3,Y           ;
    lda variables_start + 2,X           ;
    sta variables_start + 2,Y           ;
copy16
    lda variables_start + 1,X           ;
    sta variables_start + 1,Y           ;
    lda variables_start,X               ;
    sta variables_start,Y               ;
    rts                                 ;

; ***************************************************************************************
;
; Y = A-X
;
; ***************************************************************************************
subtract16
    sty result_index                    ;
    sta param1_index                    ;
    tay                                 ;

    lda variables_start,Y               ;
    sec                                 ;
    sbc variables_start,X               ;
    ldy result_index                    ;
    sta variables_start,Y               ;
    ldy param1_index                    ;
    lda variables_start + 1,Y           ;
    sbc variables_start + 1,X           ;
    ldy result_index                    ;
    sta variables_start + 1,Y           ;
    rts                                 ;

; ***************************************************************************************
;
; Y = A-X
;
; ***************************************************************************************
subtract32
    sty result_index                    ;
    sta param1_index                    ;
    tay                                 ;

    lda variables_start,Y               ;
    sec                                 ;
    sbc variables_start,X               ;
    ldy result_index                    ;
    sta variables_start,Y               ;

    ldy param1_index                    ;
    lda variables_start + 1,Y           ;
    sbc variables_start + 1,X           ;
    ldy result_index                    ;
    sta variables_start + 1,Y           ;

    ldy param1_index                    ;
    lda variables_start + 2,Y           ;
    sbc variables_start + 2,X           ;
    ldy result_index                    ;
    sta variables_start + 2,Y           ;

    ldy param1_index                    ;
    lda variables_start + 3,Y           ;
    sbc variables_start + 3,X           ;
    ldy result_index                    ;
    sta variables_start + 3,Y           ;
    rts                                 ;

; ***************************************************************************************
;
; Y = A+X
;
; ***************************************************************************************
add32
    sty result_index                    ;
    sta param1_index                    ;
    tay                                 ;

    lda variables_start,Y               ;
    clc                                 ;
    adc variables_start,X               ;
    ldy result_index                    ;
    sta variables_start,Y               ;

    ldy param1_index                    ;
    lda variables_start + 1,Y           ;
    adc variables_start + 1,X           ;
    ldy result_index                    ;
    sta variables_start + 1,Y           ;

    ldy param1_index                    ;
    lda variables_start + 2,Y           ;
    adc variables_start + 2,X           ;
    ldy result_index                    ;
    sta variables_start + 2,Y           ;

    ldy param1_index                    ;
    lda variables_start + 3,Y           ;
    adc variables_start + 3,X           ;
    ldy result_index                    ;
    sta variables_start + 3,Y           ;
    rts                                 ;

; ***************************************************************************************
;
; Y = A+X
; preserves X
;
; ***************************************************************************************
add16
    sty result_index                    ;
    sta param1_index                    ;
    tay                                 ;

    lda variables_start,Y               ;
    clc                                 ;
    adc variables_start,X               ;
    ldy result_index                    ;
    sta variables_start,Y               ;

    ldy param1_index                    ;
    lda variables_start + 1,Y           ;
    adc variables_start + 1,X           ;
    ldy result_index                    ;
    sta variables_start + 1,Y           ;

    rts                                 ;

; ***************************************************************************************
sgn16
    lda variables_start + 1,X           ;
    bmi negative                        ;
    bne positive                        ;
    ora variables_start + 0,X           ;
    bne positive                        ;
zero
    lda #0                              ;
    sta variables_start + 0,X           ;
    sta variables_start + 1,X           ;
    rts                                 ;
positive
    lda #1                              ;
    sta variables_start + 0,X           ;
    lda #0                              ;
    sta variables_start + 1,X           ;
    rts                                 ;
negative
    lda #255                            ;
    sta variables_start + 0,X           ;
    sta variables_start + 1,X           ;
    rts                                 ;

; ***************************************************************************************
;
; Y = X/2
;
; ***************************************************************************************
div2_32
    lda variables_start + 3,X           ;
    lsr                                 ;
    sta variables_start + 3,Y           ;
    lda variables_start + 2,X           ;
    ror                                 ;
    sta variables_start + 2,Y           ;
div2_16
    lda variables_start + 1,X           ;
    ror                                 ;
    sta variables_start + 1,Y           ;
    lda variables_start + 0,X           ;
    ror                                 ;
    sta variables_start + 0,Y           ;
    rts                                 ;

; ***************************************************************************************
;
; Y = A * X
;
; multiplies two 16 bit unsigned integers into a 32 bit result
;
; On Entry:
;   The registers are the byte offsets from variables_start to the variables:
;   A = multiplier      (2 bytes)
;   X = multiplicand    (2 bytes)
;   Y = result          (4 bytes)
;
; ***************************************************************************************
multiply16
    sta multiplier_index                ;
    stx multiplicand_index              ;
    sty product_index                   ;

    tax
    ldy #multiplier                     ;
    jsr copy2_to_zp                     ;

    ldx multiplicand_index              ;
    ldy #multiplicand                   ;
    jsr copy2_to_zp                     ;

    lda #16                             ; set binary count to 32
    sta counter                         ;
    lda #0                              ;
    sta product + 2                     ; clear upper half of product
    sta product + 3                     ;
-
    lsr multiplier + 1                  ; shift multiplier right
    ror multiplier + 0                  ;
    bcc +                               ; go rotate right if c = 0

    lda product + 2                     ; get upper half of product and add multiplicand to it
    clc                                 ;
    adc multiplicand                    ;
    sta product + 2                     ;

    lda product + 3                     ;
    adc multiplicand + 1                ;

+
    ror                                 ; rotate partial product right
    sta product + 3                     ;
    ror product + 2                     ;
    ror product + 1                     ;
    ror product + 0                     ;
    dec counter                         ; decrement bit count and
    bne -                               ; loop until 16 bits are done

    ; copy result (4 bytes)
    ldx product_index                   ;
    ldy #3                              ;
-
    lda product,Y                       ;
    sta variables_start + 3,X           ;
    dex                                 ;
    dey                                 ;
    bpl -                               ;
    rts                                 ;

; ***************************************************************************************
variables_start
; ***************************************************************************************
; inputs
centrex
    !word 160
centrey
    !word 128
a
    !word 58
b
    !word 100
    !word 0         ; padding (always zero), used to make multiply32 work on this variable
s
    !le32 -20

; workspace variables
x1
    !word 0
x2
    !word 0
ox1
    !word 0
ox2
    !word 0
y
    !word 0
aa
    !le32 0
bb
    !le32 0
bb2
    !le32 0
bd
    !le32 0
dd
    !le32 0
d1
    !le32 0
ys
    !le32 0
t1
    !le32 0
t2
    !le32 0
tt1
    !le32 0
tt2
    !le32 0
temp
    !le32 0
yyaa
    !le32 0
yyaa_diff
    !le32 0
aabb
    !le32 0
x1b
    !le32 0
x2b
    !le32 0
st1
    !le32 0
st2
    !le32 0
ss
    !le32 0
sb
    !le32 0
bt1
    !le32 0
bt2
    !le32 0
