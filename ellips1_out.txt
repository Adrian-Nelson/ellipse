10MODE 1
20PROCellipse(160,128,58,100,-20)
30END
40:
50DEFPROCellipse(CX%,CY%,A%,B%,S%)
60VDU 29,CX%*4;CY%*4;
70BB%=B%*B%
80AA%=A%*A%
90YYAA%=0:AABB%=AA%*BB%
100YYAA_DIFF%=AA%
110X1%=-A%:T1%=-A%*B%:TT1%=AABB%:BT1%=B%*T1%:ST1%=S%*T1%
120X2%= A%:T2%=-T1%  :TT2%=AABB%:BT2%=-BT1% :ST2%=-ST1%
130YS%=0:BB2%=BB%/2:SB%=S%*B%:SS%=S%*S%
140OY1%=0:OY2%=0:OOX1%=X1%:OOX2%=X2%:ODELTAX1%=0:ODELTAX2%=0:col1%=1:col2%=2:REM used for straight line segments
150FOR Y%=0 TO B%-1
160OX2%=X2%:OX1%=X1%
170DD%=AABB%-YYAA%
180:
190REM first half
200IF T2%<0 X2%=X2%+1:T2%=T2%+B%:TT2%=TT2%+2*BT2%+BB%:BT2%=BT2%+BB%:ST2%=ST2%+SB%:GOTO 200
210IF T2%=0 GOTO 270
220D1%=TT2%-DD%
230BD%=BT2%+BB2%
240IF BD%-D1% < 0 X2%=X2%-1:T2%=T2%-B%:TT2%=TT2%-2*BT2%+BB%:BT2%=BT2%-BB%:ST2%=ST2%-SB%:GOTO 200
250IF BD%+D1% < 0 X2%=X2%+1:T2%=T2%+B%:TT2%=TT2%+2*BT2%+BB%:BT2%=BT2%+BB%:ST2%=ST2%+SB%:GOTO 200
260:
270DELTAX2%=X2%-OX2%
280IF Y%=B%-1 GOTO 310
290IF DELTAX2%=ODELTAX2% AND ABS(DELTAX2%)<2 GOTO400:REM we are building a straight-line segment so don't draw anything for now
300IF ABS(ODELTAX2%)=1 AND DELTAX2%=0 AND Y%=OY2%+1 GOTO370:REM fix for consecutive vlines
310DX%=SGN(OX2%-OOX2%)
320REM finish off old straight-line segment:
330GCOL3,col2%:col2%=3-col2%
340MOVE  (OOX2%+DX%)*4, OY2%*4:DRAW OX2%*4, (Y%-1)*4
350MOVE -(OOX2%+DX%)*4,-OY2%*4:DRAW-OX2%*4,-(Y%-1)*4
360OY2%=Y%
370OOX2%=OX2%:ODELTAX2%=DELTAX2%
380:
390REM second half
400IF T1%>0 X1%=X1%-1:T1%=T1%-B%:TT1%=TT1%-2*BT1%+BB%:BT1%=BT1%-BB%:ST1%=ST1%-SB%:GOTO 400
410IF T1%=0 GOTO 470
420D1%=TT1%-DD%
430BD%=BB2%-BT1%
440IF BD%-D1% < 0 X1%=X1%+1:T1%=T1%+B%:TT1%=TT1%+2*BT1%+BB%:BT1%=BT1%+BB%:ST1% = ST1%+SB%:GOTO 400
450IF BD%+D1% < 0 X1%=X1%-1:T1%=T1%-B%:TT1%=TT1%-2*BT1%+BB%:BT1%=BT1%-BB%:ST1% = ST1%-SB%:GOTO 400
460:
470DELTAX1%=X1%-OX1%
480IF Y%=B%-1 GOTO 510
490IF DELTAX1%=ODELTAX1% AND ABS(DELTAX1%)<2:GOTO590:REM we are building a straight-line segment so don't draw anything for now
500IF ABS(ODELTAX1%)=1 AND DELTAX1%=0 AND Y%=OY1%+1 GOTO570:REM fix for consecutive vlines
510DX%=SGN(OX1%-OOX1%)
520REM finish off old straight-line segment:
530GCOL3,col1%:col1%=3-col1%
540MOVE  (OOX1%+DX%)*4, OY1%*4:DRAW OX1%*4,(Y%-1)*4
550MOVE -(OOX1%+DX%)*4,-OY1%*4:DRAW-OX1%*4,-(Y%-1)*4
560OY1%=Y%
570OOX1%=OX1%:ODELTAX1%=DELTAX1%
580:
590TT2%=TT2%-2*ST2%+SS%:TT1%=TT1%-2*ST1%+SS%:YS%=YS%+S%:ST2%=ST2%-SS%:ST1%=ST1%-SS%
600YYAA%=YYAA%+YYAA_DIFF%
610YYAA_DIFF%=YYAA_DIFF%+2*AA%
620BT2%=BT2%-SB%:BT1%=BT1%-SB%:T2%=T2%-S%:T1%=T1%-S%
630NEXT
640:
650Y%=B%
660REM finish off old line segments:
670DX%=SGN(X2%-OX2%)
680GCOL3,col2%:col2%=3-col2%
690MOVE  (OX2%+DX%)*4, (Y%-1)*4:DRAW X2%*4, (Y%-1)*4
700MOVE -(OX2%+DX%)*4,-(Y%-1)*4:DRAW-X2%*4,-(Y%-1)*4
710:
720DX%=SGN(X1%-OX1%)
730GCOL3,col1%:col1%=3-col1%
740MOVE  (OX1%+DX%)*4, (Y%-1)*4:DRAW X1%*4, (Y%-1)*4
750MOVE -(OX1%+DX%)*4,-(Y%-1)*4:DRAW-X1%*4,-(Y%-1)*4
760:
770REM finish off cap and tail hlines of ellipse
780GCOL3,3
790DX%=SGN(X2%-X1%):X1%=X1%+DX%:X2%=X2%-DX%
800MOVE  X1%*4, B%*4:DRAW X2%*4, B%*4
810MOVE -X1%*4,-B%*4:DRAW-X2%*4,-B%*4
820:
830ENDPROC
