10MODE 1
20PROCellipse(160,128,58,100,-20)
30END
40:
50DEFPROCellipse(CX%,CY%,A%,B%,S%)
60VDU 29,CX%*4;CY%*4;
70BB%=B%*B%
80AA%=A%*A%
90YYAA%=0:AABB%=AA%*BB%
100YYAA_DIFF%=AA%
110X1%=-A%:T1%=-A%*B%:TT1%=AABB%:BT1%=B%*T1%:ST1%=S%*T1%
120X2%= A%:T2%=-T1%  :TT2%=AABB%:BT2%=-BT1% :ST2%=-ST1%
130YS%=0:BB2%=BB%/2:SB%=S%*B%:SS%=S%*S%
140OY1%=1:OY2%=1:OOX1%=X1%:OOX2%=X2%:ODELTAX1%=0:ODELTAX2%=0:col1%=1:col2%=2:REM used for straight line segments
150PLOT 69,A%*4,0:PLOT 69,-A%*4,0
160FOR Y%=1 TO B%-1
170OX2%=X2%:OX1%=X1%
180DD%=AABB%-YYAA%
190:
200REM first half
210IF T2%<0 X2%=X2%+1:T2%=T2%+B%:TT2%=TT2%+2*BT2%+BB%:BT2%=BT2%+BB%:ST2%=ST2%+SB%:GOTO 210
220IF T2%=0 GOTO 270
230D1%=TT2%-DD%
240BD%=BT2%+BB2%
250IF BD%-D1% < 0 X2%=X2%-1:T2%=T2%-B%:TT2%=TT2%-2*BT2%+BB%:BT2%=BT2%-BB%:ST2%=ST2%-SB%:GOTO 210
260IF BD%+D1% < 0 X2%=X2%+1:T2%=T2%+B%:TT2%=TT2%+2*BT2%+BB%:BT2%=BT2%+BB%:ST2%=ST2%+SB%:GOTO 210
270DELTAX2%=X2%-OX2%
280IF Y%=B%-1 GOTO 310
290IF DELTAX2%=ODELTAX2% AND ABS(DELTAX2%)<2 GOTO390:REM we are building a straight-line segment so don't draw anything for now
300IF ABS(ODELTAX2%)=1 AND DELTAX2%=0 AND Y%=OY2%+1 ODELTAX2%=DELTAX2%:OOX2%=OX2%:GOTO390:REM fix for consecutive vlines
310DX%=SGN(OX2%-OOX2%)
320REM finish off old straight-line segment:
330GCOL3,col2%:col2%=3-col2%
340MOVE  (OOX2%+DX%)*4, OY2%*4:DRAW OX2%*4, (Y%-1)*4
350MOVE -(OOX2%+DX%)*4,-OY2%*4:DRAW-OX2%*4,-(Y%-1)*4
360OY2%=Y%:OOX2%=OX2%:ODELTAX2%=DELTAX2%
370:
380REM second half
390IF T1%>0 X1%=X1%-1:T1%=T1%-B%:TT1%=TT1%-2*BT1%+BB%:BT1%=BT1%-BB%:ST1%=ST1%-SB%:GOTO 390
400IF T1%=0 GOTO 460
410D1%=TT1%-DD%
420BD%=BB2%-BT1%
430IF BD%-D1% < 0 X1%=X1%+1:T1%=T1%+B%:TT1%=TT1%+2*BT1%+BB%:BT1%=BT1%+BB%:ST1% = ST1%+SB%:GOTO 390
440IF BD%+D1% < 0 X1%=X1%-1:T1%=T1%-B%:TT1%=TT1%-2*BT1%+BB%:BT1%=BT1%-BB%:ST1% = ST1%-SB%:GOTO 390
450:
460DELTAX1%=X1%-OX1%
470IF Y%=B%-1 GOTO 500
480IF DELTAX1%=ODELTAX1% AND ABS(DELTAX1%)<2:GOTO570:REM we are building a straight-line segment so don't draw anything for now
490IF ABS(ODELTAX1%)=1 AND DELTAX1%=0 AND Y%=OY1%+1:ODELTAX1%=DELTAX1%:OOX1%=OX1%:GOTO570:REM fix for consecutive vlines
500DX%=SGN(OX1%-OOX1%)
510REM finish off old straight-line segment:
520GCOL3,col1%:col1%=3-col1%
530MOVE  (OOX1%+DX%)*4, OY1%*4:DRAW OX1%*4,(Y%-1)*4
540MOVE -(OOX1%+DX%)*4,-OY1%*4:DRAW-OX1%*4,-(Y%-1)*4
550OY1%=Y%:OOX1%=OX1%:ODELTAX1%=DELTAX1%
560:
570TT2%=TT2%-2*ST2%+SS%:TT1%=TT1%-2*ST1%+SS%:YS%=YS%+S%:ST2%=ST2%-SS%:ST1%=ST1%-SS%
580YYAA%=YYAA%+YYAA_DIFF%
590YYAA_DIFF%=YYAA_DIFF%+2*AA%
600BT2%=BT2%-SB%:BT1%=BT1%-SB%:T2%=T2%-S%:T1%=T1%-S%
610NEXT
620:
630Y%=B%
640REM finish off old line segments:
650DX%=SGN(X2%-OX2%)
660GCOL3,col2%:col2%=3-col2%
670MOVE  (OX2%+DX%)*4, (Y%-1)*4:DRAW X2%*4, (Y%-1)*4
680MOVE -(OX2%+DX%)*4,-(Y%-1)*4:DRAW-X2%*4,-(Y%-1)*4
690:
700DX%=SGN(X1%-OX1%)
710GCOL3,col1%:col1%=3-col1%
720MOVE  (OX1%+DX%)*4, (Y%-1)*4:DRAW X1%*4, (Y%-1)*4
730MOVE -(OX1%+DX%)*4,-(Y%-1)*4:DRAW-X1%*4,-(Y%-1)*4
740:
750REM finish off cap and tail hlines of ellipse
760GCOL3,3
770DX%=SGN(X2%-X1%):X1%=X1%+DX%:X2%=X2%-DX%
780MOVE  X1%*4, B%*4:DRAW X2%*4, B%*4
790MOVE -X1%*4,-B%*4:DRAW-X2%*4,-B%*4
800:
810ENDPROC
