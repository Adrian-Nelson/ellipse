10MODE 1
20PROCellipse(160,128,58,100,-20)
30END
40:
50DEFPROCellipse(CX%,CY%,A%,B%,S%)
60VDU 29,CX%*4;CY%*4;
70BB%=B%*B%
80AA%=A%*A%
90YYAA%=0:AABB%=AA%*BB%
100YYAA_DIFF%=AA%
110X1%=-A%:T1%=-A%*B%:TT1%=AABB%:BT1%=B%*T1%:ST1%=S%*T1%
120X2%= A%:T2%=-T1%  :TT2%=AABB%:BT2%=-BT1% :ST2%=-ST1%
130YS%=0:BB2%=BB%/2:SB%=S%*B%:SS%=S%*S%
140OY1%=0:OY2%=0:OOX1%=X1%:OOX2%=X2%:ODELTAX1%=0:ODELTAX2%=0:col1%=1:col2%=2:REM used for straight line segments
150FOR Y%=0 TO B%-1
160OX2%=X2%:OX1%=X1%
170DD%=AABB%-YYAA%
180:
190REM first half
200IF T2%<0 X2%=X2%+1:T2%=T2%+B%:TT2%=TT2%+2*BT2%+BB%:BT2%=BT2%+BB%:ST2%=ST2%+SB%:GOTO 200
210IF T2%=0 GOTO 260
220D1%=TT2%-DD%
230BD%=BT2%+BB2%
240IF BD%-D1% < 0 X2%=X2%-1:T2%=T2%-B%:TT2%=TT2%-2*BT2%+BB%:BT2%=BT2%-BB%:ST2%=ST2%-SB%:GOTO 200
250IF BD%+D1% < 0 X2%=X2%+1:T2%=T2%+B%:TT2%=TT2%+2*BT2%+BB%:BT2%=BT2%+BB%:ST2%=ST2%+SB%:GOTO 200
260DELTAX2%=X2%-OX2%
270IF Y%=B%-1 GOTO 300
280IF DELTAX2%=ODELTAX2% AND ABS(DELTAX2%)<2 GOTO380:REM we are building a straight-line segment so don't draw anything for now
290IF ABS(ODELTAX2%)=1 AND DELTAX2%=0 AND Y%=OY2%+1 ODELTAX2%=DELTAX2%:OOX2%=OX2%:GOTO380:REM fix for consecutive vlines
300DX%=SGN(OX2%-OOX2%)
310REM finish off old straight-line segment:
320GCOL3,col2%:col2%=3-col2%
330MOVE  (OOX2%+DX%)*4, OY2%*4:DRAW OX2%*4, (Y%-1)*4
340MOVE -(OOX2%+DX%)*4,-OY2%*4:DRAW-OX2%*4,-(Y%-1)*4
350OY2%=Y%:OOX2%=OX2%:ODELTAX2%=DELTAX2%
360:
370REM second half
380IF T1%>0 X1%=X1%-1:T1%=T1%-B%:TT1%=TT1%-2*BT1%+BB%:BT1%=BT1%-BB%:ST1%=ST1%-SB%:GOTO 380
390IF T1%=0 GOTO 450
400D1%=TT1%-DD%
410BD%=BB2%-BT1%
420IF BD%-D1% < 0 X1%=X1%+1:T1%=T1%+B%:TT1%=TT1%+2*BT1%+BB%:BT1%=BT1%+BB%:ST1% = ST1%+SB%:GOTO 380
430IF BD%+D1% < 0 X1%=X1%-1:T1%=T1%-B%:TT1%=TT1%-2*BT1%+BB%:BT1%=BT1%-BB%:ST1% = ST1%-SB%:GOTO 380
440:
450DELTAX1%=X1%-OX1%
460IF Y%=B%-1 GOTO 490
470IF DELTAX1%=ODELTAX1% AND ABS(DELTAX1%)<2:GOTO560:REM we are building a straight-line segment so don't draw anything for now
480IF ABS(ODELTAX1%)=1 AND DELTAX1%=0 AND Y%=OY1%+1:ODELTAX1%=DELTAX1%:OOX1%=OX1%:GOTO560:REM fix for consecutive vlines
490DX%=SGN(OX1%-OOX1%)
500REM finish off old straight-line segment:
510GCOL3,col1%:col1%=3-col1%
520MOVE  (OOX1%+DX%)*4, OY1%*4:DRAW OX1%*4,(Y%-1)*4
530MOVE -(OOX1%+DX%)*4,-OY1%*4:DRAW-OX1%*4,-(Y%-1)*4
540OY1%=Y%:OOX1%=OX1%:ODELTAX1%=DELTAX1%
550:
560TT2%=TT2%-2*ST2%+SS%:TT1%=TT1%-2*ST1%+SS%:YS%=YS%+S%:ST2%=ST2%-SS%:ST1%=ST1%-SS%
570YYAA%=YYAA%+YYAA_DIFF%
580YYAA_DIFF%=YYAA_DIFF%+2*AA%
590BT2%=BT2%-SB%:BT1%=BT1%-SB%:T2%=T2%-S%:T1%=T1%-S%
600NEXT
610:
620Y%=B%
630REM finish off old line segments:
640DX%=SGN(X2%-OX2%)
650GCOL3,col2%:col2%=3-col2%
660MOVE  (OX2%+DX%)*4, (Y%-1)*4:DRAW X2%*4, (Y%-1)*4
670MOVE -(OX2%+DX%)*4,-(Y%-1)*4:DRAW-X2%*4,-(Y%-1)*4
680:
690DX%=SGN(X1%-OX1%)
700GCOL3,col1%:col1%=3-col1%
710MOVE  (OX1%+DX%)*4, (Y%-1)*4:DRAW X1%*4, (Y%-1)*4
720MOVE -(OX1%+DX%)*4,-(Y%-1)*4:DRAW-X1%*4,-(Y%-1)*4
730:
740REM finish off cap and tail hlines of ellipse
750GCOL3,3
760DX%=SGN(X2%-X1%):X1%=X1%+DX%:X2%=X2%-DX%
770MOVE  X1%*4, B%*4:DRAW X2%*4, B%*4
780MOVE -X1%*4,-B%*4:DRAW-X2%*4,-B%*4
790:
800ENDPROC
